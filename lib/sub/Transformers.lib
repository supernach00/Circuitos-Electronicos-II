.subckt CoreJA 100 ms=381.82k a=28.067 k=18.57 c=0.24753 
+ area=24u  LeN=32m
.Subckt Winding1 l1 l2 mmf_out2 mmf_out 
.param  rs=1m Tau=5n Tau2=5n
B1 5 l1  i=v(7)/Rs Rpar={Rs} Cpar={tau2/rs}
v1 5 l2 dc 0
bH2  mmf_out 6 v=i(v1)
v2  6 mmf_out2  dc  0 
BF  7   0 i=i(v2) tripdt=50n
L  7  0  1 Rpar={1/tau} Cpar=.1f 
.ends Winding1

.param RS=1m 
.param c1=1/(1+c) c2=c1*C   
.param mU0=1.256637061436u
.func f(x) IF(aBS(x)>1m,(1/taNh(x)-1/x),0.33333*x)
b1 3 0 V=V(m)/LeN
b2 4 0  V=f(v(3)/A)
b3 5 0 v=v(m)
C1 5 7 1m rser={rs}  ic=0
V1 7 0 0
bi4 0 9 i=k*SGN(I(V1))  rpar=1 Cpar=1n
b5 11 0 v=Uramp(V(4,12)/((V(9)+1m)*len))
b6 12 0 V=idt(1000*I(V1)*v(11)*c1)+c2*V(4)  tripdt=5n
b7 13 0 V=aRea*mU0*(mS*V(12)+V(3))
bi1 m 0 i=v(13) 
xw 100 0 m 0 Winding1
.ends CoreJA


.subckt Winding_LCR 1 2 core Lser=1n Rser=1m  Cpar=1p N=1
L 2 3 {Lser} Rser={Rser}
C 1 2 {Cpar}
E 1 3 core 0 {N}
F 0   core E {N}
.ends Winding_LCR

.subckt Winding_RC 1 2 core  Rser=1m  Cpar=1p N=1
R 2 3 {Rser}
C 1 2 {Cpar}
E 1 3 core 0 {N}
F 0   core E {N}
.ends Winding_RC

.subckt core core Hc=5 Bs=460m Br=100m A=14u Lm=31m Lg=0 Fe=10Meg
.param u0u={2*Bs*Br/(Bs+Br)/Hc}
.param L={u0u*A/Lm}
.param Rpar={L*2*Pi*Fe}
L core 0 Hc={Hc} Bs={Bs} Br={Br} A={A} Lm={Lm} Lg={Lg} N=1 Rser=0 Rpar={Rpar} 
.ends core

.subckt core_B core B Hc=5 Bs=460m Br=100m A=14u Lm=31m Lg=0 Fe=10Meg B0=0
.param u0u={2*Bs*Br/(Bs+Br)/Hc}
*.param mu={u0u/1.2566e-6}
*.param AL={A/Lm*(u0u/(1+mU*Lg/Lm))}
.param AL={A/Lm*u0u}
.param Rpar={AL*2*Pi*Fe}
L core 0 Hc={Hc} Bs={Bs} Br={Br} A={A} Lm={Lm} Lg={Lg} N=1 Rser=0 Rpar={Rpar}
B B 0 V=idt(V(core),B0*A)/A 
.ends core_B

.subckt core_B_H core B H Hc=5 Bs=460m Br=100m A=14u Lm=31m Lg=0 Fe=10Meg B0=0
.param u0u={2*Bs*Br/(Bs+Br)/Hc}
*.param mu={u0u/1.2566e-6}
*.param AL={A/Lm*(u0u/(1+mU*Lg/Lm))}
.param AL={A/Lm*u0u}
.param Rpar={AL*2*Pi*Fe}
L core 0 Hc={Hc} Bs={Bs} Br={Br} A={A} Lm={Lm} Lg={Lg} N=1 Rser=0 Rpar={Rpar}
B B 0 V=idt(V(core),B0*A)/A 
B2 H 0 V=I(L)/Lm 
.ends core_B_H

.subckt coreLin_AL core  AL=1u  Fe=100Meg
L core 0 {AL} Rser=0 Rpar={2*Pi*Fe*AL}
.ends corecoreLin_AL

.subckt coreLin_A_Lm core  mU=2000 A=14u Lm=31m Lg=0 Fe=10Meg
.param AL={1.2566e-6*A/Lm*(mU/(1+mU*Lg/Lm))}
L core 0 {AL} Rser=0 Rpar={2*Pi*Fe*AL}
.ends coreLin_A_Lm